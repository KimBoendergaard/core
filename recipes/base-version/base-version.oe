## Recipe that Creates version, buildtime, buildtag for a rootfs.
##
## This recipe is used for keeping track of a rootfs. It inserts files into
## the rootfs, that describe the current version of the rootfs and
## additional build information.
##
## @useflag basefiles_manifest_version Append dirty if dirty to
##          basefiles_version file.
## @useflag basefiles_version Set to path of a version file. If set, goes in
##          rootfs.
## @useflag basefiles_buildtime Set to path of a buildtime file. If set, goes
##          in rootfs.
## @useflag basefiles_cross_config Set to path of a config file. If set, goes
##          in rootfs.
## @useflag basefiles_buildtag Set to path of a buildtag file. If set, goes
##          in rootfs.

# -*- mode:python; -*-
DESCRIPTION = "Distroversion and build-time file creation."
LICENSE = "N/A"

PACKAGES = "${PN}"

do_install () {
	:
}

do_install[postfuncs] += "${BASE_VERSION_POSTFUNCS}"
BASE_VERSION_POSTFUNCS ?= ""

BASE_VERSION ?= "${DISTRO_VERSION}"

inherit manifest-version
OESTACK_VERSION[nohash] = "1"
RECIPE_FLAGS += "basefiles_manifest_version"
BASE_VERSION:USE_basefiles_manifest_version = "${OESTACK_VERSION}"

DEFAULT_USE_basefiles_manifest_version = True

RECIPE_FLAGS += "basefiles_version"
BASE_VERSION_POSTFUNCS:>USE_basefiles_version = " do_install_basefiles_version"
do_install_basefiles_version[expand] = 3
do_install_basefiles_version () {
	echo "${BASE_VERSION}" > \
	      ${SRCDIR}/${USE_basefiles_version}
	install -m 0755 -d ${D}${sysconfdir}
	install -m 0644 ${SRCDIR}/${USE_basefiles_version} \
		${D}${sysconfdir}/${USE_basefiles_version}
}


RECIPE_FLAGS += "basefiles_buildtime"
BASE_VERSION_POSTFUNCS:>USE_basefiles_buildtime = " do_install_basefiles_buildtime"
do_install_basefiles_buildtime[expand] = 3
do_install_basefiles_buildtime () {
	echo "${DATETIME}" > \
		${SRCDIR}/${USE_basefiles_buildtime}

	install -m 0755 -d ${D}${sysconfdir}
	install -m 0644 ${SRCDIR}/${USE_basefiles_buildtime} \
		${D}${sysconfdir}/${USE_basefiles_buildtime}
}

export BUILD_TAG
RECIPE_FLAGS += "basefiles_buildtag"
BASE_VERSION_POSTFUNCS:>USE_basefiles_buildtag = " do_install_basefiles_buildtag"
DEFAULT_USE_basefiles_buildtag = "build_tag"
do_install_basefiles_buildtag () {
	if [ -n "$BUILD_TAG" ]; then
		echo "$BUILD_TAG" > ${SRCDIR}/${USE_basefiles_buildtag}
		install -m 0755 -d ${D}${sysconfdir}
		install -m 0644 ${SRCDIR}/${USE_basefiles_buildtag} \
			${D}${sysconfdir}/${USE_basefiles_buildtag}
	fi
}

# Local Variables:
# mode: python
# End:
