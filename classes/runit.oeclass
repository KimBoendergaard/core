## OE-lite class for handling runit services
##
## This class handles runit services. If having a recipe with a runit service,
## place that script in ${SRCDIR}, and make sure that the necessary useflags
## is setup. The necessary use flags are:
## <chosen name>_runit (declare this to RECIPE_FLAGS, and set to true)
## <chosen name> (declare this to RECIPE_FLAGS and set to true)
## the needed variables are:
## RUNIT_NAME_<chosen name> Set this to the service name.
## RUNIT_SCRIPT_<chosen name> Set this to the filename of the service.
##
## @useflag runit Enable or disable runit. Disabled by default.
##
## @var RDEPENDS_RUNIT Set the rdependency of runit. Default set to runit.

CLASS_FLAGS += "runit"

RDEPENDS_RUNIT ?= "runit"
RDEPENDS_${PN}:>USE_runit = " ${RDEPENDS_RUNIT}"

do_install[postfuncs] += "do_install_runit"
python do_install_runit () {
    import stat
    path = d.get("D")
    os.chdir(path)

    if not bb.data.getVar('USE_runit', d, True):
        return

    options = ((d.get("RECIPE_FLAGS") or "").split() +
               (d.get("CLASS_FLAGS") or "").split())
    runitservicedir = bb.data.getVar('runitservicedir', d, True)
    srcdir = bb.data.getVar('SRCDIR', d, True)

    for option in options:

        if not option.endswith('_runit'):
            continue
        
        enable = d.get("USE_"+option)
        if not enable:
            continue

	name = option[0:-len('_runit')]
        svname = bb.data.getVar('RUNIT_NAME_'+name, d, True)
        if not svname:
            svname = name.replace('_', '-')

        script = bb.data.getVar('RUNIT_SCRIPT_'+name, d, True)
        if not script:
            script = srcdir + '/' + svname + '.runit'

        dst_dir = '.%s/%s'%(runitservicedir, svname)
	dst = dst_dir + '/run'

        if not os.path.exists(script):
            bb.note('runit script not found: %s'%script)
            continue

        if not os.path.exists(dst_dir):
            os.makedirs(dst_dir)
        shutil.copy(script, dst)
	os.chmod(dst, stat.S_IRWXU|stat.S_IRGRP|stat.S_IXGRP|stat.S_IROTH|stat.S_IXOTH)

        down = d.get('USE_%s_down'%(option))
        if down:
            open(os.path.join(dst_dir, 'down'), 'w').close()
}

# Local Variables:
# mode: python
# End:
