## Class for creating kernel modules.
##
## Uses for recipes compiling kernel modules that ofcause needs a kernel to be
## build against.

RECIPE_TYPES = "machine"
COMPATIBLE_MACHINES = ".*"

inherit c kbuild kernel-arch kernel-modules-strip

KERNEL_MODULES_DEPENDS ?= "kernel-dev"
CLASS_DEPENDS += "${KERNEL_MODULES_DEPENDS}"

EXTRA_OEMAKE += "CROSS_COMPILE=${TARGET_PREFIX}"

KERNEL_MODULES_COMPILE_MAKE_TARGETS ?= "modules"

kernel_modules_do_compile () {
	unset CFLAGS CPPFLAGS CXXFLAGS LDFLAGS
	oe_runmake \
		KERNELDIR=${HOST_SYSROOT}${kernelsrcdir} \
		ARCH=${KERNEL_ARCH} \
		${KERNEL_MODULES_COMPILE_MAKE_TARGETS}
}

do_compile () {
	kernel_modules_do_compile
}

KERNEL_MODULES_INSTALL_MAKE_TARGETS ?= "modules_install"

kernel_modules_do_install() {
	unset CFLAGS CPPFLAGS CXXFLAGS LDFLAGS
	oe_runmake V=1 \
		DEPMOD=echo INSTALL_MOD_PATH="${D}" \
		KERNELDIR=${HOST_SYSROOT}${kernelsrcdir} \
		ARCH=${KERNEL_ARCH} \
		${KERNEL_MODULES_INSTALL_MAKE_TARGETS}
}

do_install () {
	kernel_modules_do_install
}

PACKAGES += "${PN}-modules"
FILES_${PN}-modules = "/lib/modules"

# Local Variables:
# mode: python
# End:
