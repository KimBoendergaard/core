## Places tarballs ontop of existing rootfs.
##
## This class is usefull when building a tarball image to be placed in
## an already existing rootfs. It does not contains the same libraries
## as in the rootfs, but only the delta to the rootfs.
##
## @useflag rootfs_name The name of the rootfs recipe in which this package will
##          be placed.
## @useflag basefiles_version Set to a path to a version file if not wanting to
##          remove it if already present in the rootfs.
## @useflag basefiles_buildtime Set to a path to a buildtime file if not
##          wanting to remove it if already present in the rootfs.
## @useflag basefiles_buildtag Set to a path to a buildtag if not wanting to
##          remove it if already present in the rootfs.

inherit archive-image fakeroot

IMAGE_BASENAME = "${PN}"

CLASS_FLAGS += "rootfs_name rootfs_archive_tar_ext\
			basefiles_version basefiles_buildtime \
			basefiles_cross_config basefiles_buildtag"

DEFAULT_USE_rootfs_name = "base-rootfs"

DEFAULT_USE_basefiles_version = ""
DEFAULT_USE_basefiles_buildtime = ""
DEFAULT_USE_basefiles_buildtag = "build_tag"

CLASS_DEPENDS += "${USE_rootfs_name}"
CLASS_RDEPENDS += "base-version"

addtask imageqaprep before configure after stage
do_imageqaprep[dirs] = "${WORKDIR}/image-qa"
do_imageqaprep[cleandirs] = "${WORKDIR}/image-qa"
fakeroot do_imageqaprep () {
    tar xf ${TARGET_SYSROOT}/${USE_rootfs_name}.${USE_rootfs_archive_tar_ext}
}

inherit image-qa
IMAGEQA_HOST_READELF_LIB_DIRS += "\
${WORKDIR}/image-qa/${base_libdir} \
${WORKDIR}/image-qa/${libdir}"

addtask removedoubles before configure after imageqaprep rstage
do_removedoubles[dirs] = "${RSTAGE_DIR}"
do_removedoubles () {
	for file in $(find *)
	do
		if [ ! -d $file ]; then
			if [ -f ${WORKDIR}/image-qa/$file ] || [ -h ${WORKDIR}/image-qa/$file ]; then
				if ([ -n ${USE_basefiles_version} ] && [ $(basename $file) = ${USE_basefiles_version} ]) ||
				  ([ -n ${USE_basefiles_buildtime} ] && [ $(basename $file) = ${USE_basefiles_buildtime} ]) ||
				  ([ -n ${USE_basefiles_buildtag} ] && [ $(basename $file) = ${USE_basefiles_buildtag} ]); then
					oenote "Not removing build information: "$file
				else
					oenote "Removing file since its already in rootfs: "$file
					rm $file
				fi
			fi
		fi
	done
}

# Local Variables:
# mode: python
# End:
